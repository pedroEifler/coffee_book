plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'br.com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

processResources {
	dependsOn 'copyFrontToBuild'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

task copyFrontToBuild(type: Copy) {
	dependsOn 'npmBuild'
	from "$projectDir/src/front/dist"
	into "$buildDir/resources/main/static"
}

task npmBuild(type: Exec) {
	workingDir './src/front/'
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine 'npm.cmd', 'run', 'build'
	} else {
		commandLine 'npm', 'run', 'build'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
